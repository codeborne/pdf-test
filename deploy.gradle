apply from: 'build.gradle'
apply plugin: 'signing'

defaultTasks 'clean', 'test', 'install', 'uploadArchives'

signing {
  sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: project.version.endsWith("-SNAPSHOT") ? 
                            'https://oss.sonatype.org/content/repositories/snapshots/' : 
                            'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
              authentication(userName: "$sonatypeUsername", password: "$sonatypePassword")
            }

            pom.project {
              name archivesBaseName
              packaging 'jar'
              description 'PDF Test: PDF testing library'
              url 'https://github.com/codeborne/pdf-test'

               scm {
                   url 'scm:git@github.com:codeborne/pdf-test.git'
                   connection 'scm:git@github.com:codeborne/pdf-test.git'
                   developerConnection 'scm:git@github.com:codeborne/pdf-test.git'
               }

               licenses {
                   license {
                       name 'LGPL 3.0'
                       url 'http://www.gnu.org/licenses/lgpl.html'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'asolntsev'
                       name 'Andrei Solntsev'
                   }
               }
           }

           //mess with the generated pom to set the 'packaging' tag
           pom.withXml { XmlProvider xmlProvider ->
               def xml = xmlProvider.asString()
               def pomXml = new XmlParser().parse(new ByteArrayInputStream(xml.toString().bytes))

               pomXml.version[0] + { packaging('jar') }

               def newXml = new StringWriter()
               def printer = new XmlNodePrinter(new PrintWriter(newXml))
               printer.preserveWhitespace = true
               printer.print(pomXml)
               xml.setLength(0)
               xml.append(newXml.toString())
           }
        }
    }
}
