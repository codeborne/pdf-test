jar {
  manifest {
    attributes(
      "Automatic-Module-Name": project.group + '.' + project.name.replace('-', '.'),
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": archiveVersion,
      "Implementation-Vendor": "Codeborne")
  }
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allJava
}

javadoc {
  options.encoding = 'UTF-8'
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

if (project.hasProperty("signing.keyId")) {
  apply plugin: 'signing'
  apply plugin: 'maven-publish'

  signing {
    afterEvaluate {
      sign publishing.publications.mavenJava
    }
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
  }

  publishing {
    repositories {
      maven {
        name 'Maven'
        url project.version.endsWith("-SNAPSHOT") ?
          'https://oss.sonatype.org/content/repositories/snapshots/' :
          'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        credentials {
          username "$sonatypeUsername"
          password "$sonatypePassword"
        }
      }
    }
    publications {
      mavenJava(MavenPublication) {
        groupId "${project.group}"
        artifactId "${project.name}"

        from components.java
        artifact(sourcesJar)
        artifact(javadocJar)

        pom {
          name = archivesBaseName
          description = 'PDF Test: PDF testing library'
          url = 'https://github.com/codeborne/pdf-test'
          licenses {
            license {
              name = 'MIT'
              url = 'https://opensource.org/licenses/MIT'
            }
          }
          developers {
            developer {
              id = 'asolntsev'
              name = 'Andrei Solntsev'
            }
          }
          scm {
            connection = 'scm:git@github.com:codeborne/pdf-test.git'
            developerConnection = 'scm:git@github.com:codeborne/pdf-test.git'
            url = 'https://github.com/codeborne/pdf-test'
          }
        }
      }
    }
  }
}
